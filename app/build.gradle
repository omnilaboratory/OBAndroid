apply plugin: 'com.android.application'
// butterKnife必须（哪里使用放哪里）
apply plugin: 'com.jakewharton.butterknife'
// Needed for gRPC
apply plugin: 'com.google.protobuf'

android {
    def versions = rootProject.ext.versions
    compileSdkVersion versions.compileVersion
    buildToolsVersion versions.buildToolVersion
    ndkVersion "20.0.5594570"
    defaultConfig {
        multiDexEnabled = true
        applicationId versions.applicationId
        minSdkVersion versions.minSdkVersion
        targetSdkVersion versions.targetSdkVersion
        versionCode versions.versionCode
        versionName versions.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // 阿里巴巴ARouter
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
    }

    // 防止打包过程中出现OOM
    dexOptions {
        // 该属性已经被弃用  去掉
        // incremental true
        javaMaxHeapSize "4g"
    }
    // 设置签名文件
    signingConfigs {
        release {
            storeFile file('omniwallet.jks')
            storePassword "111111"
            keyAlias "XFLProperty"
            keyPassword "111111"
        }
    }

    buildTypes {
        release {
            // 定义代码混淆文件 注意：proguard-rules.pro要放在主工程的目录下
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // 是否混淆代码
            minifyEnabled true
            //是否zip对齐
            zipAlignEnabled true
            // 缩减resource文件
            shrinkResources true
            // 设置打包后应用名称
            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    if (outputFileName.endsWith('.apk')) {
                        if ("debug".equalsIgnoreCase(variant.buildType.name)) {
                            outputFileName = "OmniWallet_${getTime()}_${defaultConfig.versionName}_Test.apkk"
                        } else if ("release".equalsIgnoreCase(variant.buildType.name)) {
                            outputFileName = "OmniWallet_${getTime()}_${defaultConfig.versionName}.apk"
                        }
                    }
                }
            }
            signingConfig signingConfigs.release
        }
        debug {
            // 定义代码混淆文件 注意：proguard-rules.pro要放在主工程的目录下
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // 是否混淆代码
            minifyEnabled false
            // 设置打包后应用名称
            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    if (outputFileName.endsWith('.apk')) {
                        if ("debug".equalsIgnoreCase(variant.buildType.name)) {
                            outputFileName = "OmniWallet_${getTime()}_${defaultConfig.versionName}_Test.apk"
                        } else if ("release".equalsIgnoreCase(variant.buildType.name)) {
                            outputFileName = "OmniWallet_${getTime()}_${defaultConfig.versionName}.apk"
                        }
                    }
                }
            }
            signingConfig signingConfigs.release
        }
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

//打包时，APK文件名使用的获取时间的方法
def static getTime() {
    return new Date().format("yyMMdd", TimeZone.getDefault())
}

//  Needed for gRPC
sourceSets {
    main.java.srcDirs += "${protobuf.generatedFilesBaseDir}/main/javalite"
}

//  Needed for gRPC
protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.13.0"
    }
    plugins {
        javalite { artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0" }
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.42.1'
        }
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
            task.plugins {
                grpc {
                    // This will generate the ServiceGrpc
                    // Options added to --grpc_out
                    option 'lite'
                }
            }
        }
    }
}

dependencies {
    def dependencies = rootProject.ext.deps
    implementation fileTree(include: ['*.jar','*.aar'], dir: 'libs')
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation dependencies.junit
    androidTestImplementation dependencies.test
    androidTestImplementation(dependencies.testEspresso) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    implementation project(':baselibrary')
    implementation project(':framelibrary')
    implementation project(':thirdsupport')
    // ARouter
    annotationProcessor dependencies.ArouterAnnotation
    // butterKnife
    compile dependencies.butterKnife
    // butterKnife注解处理器
    annotationProcessor dependencies.butterKnifeAnnotation
    // 分包
    compile dependencies.multidex
    // gRPC implementation to communicate with lnd
    implementation 'io.grpc:grpc-okhttp:1.42.1'
    implementation 'io.grpc:grpc-protobuf-lite:1.42.1'
    implementation 'io.grpc:grpc-stub:1.42.1'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'com.google.code.gson:gson:2.8.7'
    compileOnly 'com.google.api.grpc:googleapis-common-protos:0.0.3'
    // Asynchronous programming library
    implementation "io.reactivex.rxjava3:rxandroid:3.0.0"
}
